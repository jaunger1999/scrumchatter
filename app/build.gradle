apply plugin: 'com.android.application'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
    }
}

apply plugin: 'com.github.ben-manes.versions'

android {
    File signFile = rootProject.file('signing.properties')
    if (signFile.exists()) {
        Properties p = new Properties();
        p.load(new FileInputStream(signFile))

        signingConfigs {
            release {
                keyAlias p.KeyAlias
                keyPassword p.KeyPassword
                storeFile file(p.StoreFile)
                storePassword p.StorePassword
            }
        }
        android.buildTypes.release.signingConfig =signingConfigs.release
    } else  {
        android.buildTypes.release.signingConfig = null
    }
    compileSdkVersion 31
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
    defaultConfig {
        applicationId "ca.rmen.android.scrumchatter"
        versionCode 10606
        versionName "1.6.6"
        minSdkVersion 14
        targetSdkVersion 31
        return true // Avoid "Not all execution paths return a value"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
    }
    return true
}

dependencies {
    def preference_version = "1.2.0"

    // Java language implementation
    implementation "androidx.preference:preference:$preference_version"
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0'

    implementation 'com.google.android.material:material:1.0.0'

    //implementation('com.ts:auth-control-sdk:5.1.1@aar') { transitive=true }

    implementation 'com.github.lecho:hellocharts-library:1.5.8@aar'
    //implementation "com.androidx.support:appcompat-v7:${rootProject.supportLibVersion}"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation ('net.sourceforge.jexcelapi:jxl:2.6.12') {
        exclude group: "log4j"
    }
    implementation "io.reactivex.rxjava2:rxjava:2.2.9"
    implementation "io.reactivex.rxjava2:rxandroid:2.0.2"
    implementation 'org.apmem.tools:layouts:1.10@aar'
    implementation 'androidx.annotation:annotation:1.3.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation "androidx.recyclerview:recyclerview:1.2.1"
}

// Only show real releases with the ben-manes plugin.
dependencyUpdates.resolutionStrategy = {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'alpha-preview', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}
